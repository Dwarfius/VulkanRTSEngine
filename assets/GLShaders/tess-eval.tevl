#version 420
#extension GL_ARB_separate_shader_objects : enable

layout(quads, fractional_even_spacing, cw) in;

layout (std140, binding = 0) uniform UniformAdapter // TODO: give it a proper name!
{
	mat4 Model;
	mat4 mvp;
};

layout (std140, binding = 1) uniform TerrainAdapter
{
	vec3 GridOrigin;
	int TileSize;
	int GridWidth;
	int GridHeight;
	float YScale;
};

layout(location = 0) in DataIn
{
	mediump vec2 TexCoords;
	int TessLevel;
} In[];

layout(location = 0) out DataOut
{
	mediump vec2 TexCoords;
	flat int TessLevel;
} Out;

uniform sampler2D TexHeightmap;

void main()
{
	// only have 1 control point
	vec3 pos = gl_in[0].gl_Position.xyz;
    pos.xz += (gl_TessCoord.xy - 0.5f) * TileSize;

    vec2 uv = In[0].TexCoords + (vec2(1.f / GridWidth, 1.f / GridHeight) * gl_TessCoord.xy);
    pos.y = texture2D(TexHeightmap, uv).x * YScale;
    Out.TexCoords = uv;
    Out.TessLevel = In[0].TessLevel;

	gl_Position = mvp * vec4(pos, 1);
}